import net.fabricmc.loom.task.RemapJarTask

plugins {
    id('fabric-loom') version('1.4-SNAPSHOT')
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings loom.officialMojangMappings()
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    implementation(project(':vanilla'))
}

tasks.withType(ProcessResources).configureEach {
    filesMatching(['fabric.mod.json', "${project_id}.mixins.fabric.json".toString()]) {
        expand project.properties
    }
}

loom {
    mixin {
        defaultRefmapName.set("${project_id}.mixins.fabric.refmap.json")
    }
}

shadowJar {
    dependencies {
        include(project(':vanilla'))
        exclude('/mappings/*')
    }
    relocate("${group_id}.${project_id}.vanilla", "${group_id}.${project_id}.vanilla.fabric")
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn(tasks.shadowJar)
    input = tasks.shadowJar.archiveFile
    destinationDirectory = file('../../target')
    archiveFileName = "${project_id}-fabric-${version}.jar"
}

tasks.build.dependsOn(tasks.remappedShadowJar)
