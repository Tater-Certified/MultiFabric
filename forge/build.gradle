plugins {
    id('net.minecraftforge.gradle') version('[6.0,6.2)')
    id('org.spongepowered.mixin') version('0.7.+')
}

minecraft {
    mappings(channel: mapping_channel, version: mapping_version)
}

dependencies {
    minecraft("net.minecraftforge:forge:${minecraft_version}-${forge_version}")
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor')
    implementation(project(':vanilla'))
}

mixin {
    add(sourceSets.main, "${project_id}.mixins.forge.refmap.json")
    config("${project_id}.mixins.forge.json")
}

tasks.withType(ProcessResources).configureEach {
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta', "${project_id}.mixins.forge.json".toString()]) {
        expand project.properties
    }
}

shadowJar {
    dependencies {
        include(project(":vanilla"))
    }
    relocate("${group_id}.${project_id}.vanilla", "${group_id}.${project_id}.vanilla.forge")
    destinationDirectory = file('../../target')
    archiveFileName = "${project_id}-forge-${version}.jar"
}

reobf {
    shadowJar {}
}

tasks.reobfShadowJar.dependsOn('shadowJar')
tasks.assemble.dependsOn('reobfShadowJar')
